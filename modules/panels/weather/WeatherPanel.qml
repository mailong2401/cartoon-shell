import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Quickshell
import Quickshell.Wayland
import Quickshell.Io
import "../../../services" as Services
import "." as Com

PanelWindow {
    id: weatherPanel

    property var theme: currentTheme
    property var lang: currentLanguage

    Services.JsonEditor {
        id: panelConfig
        filePath: Qt.resolvedUrl("../../../config/configs/" + currentConfigProfile + ".json")
        Component.onCompleted: {
            panelConfig.load(panelConfig.filePath)
            weatherPanel.apiKey = panelConfig.get("weatherApiKey", "")
            weatherPanel.location = panelConfig.get("weatherLocation", "Ho Chi Minh,VN")
            if (weatherPanel.apiKey !== "") {
                weatherPanel.updateWeather()
            }
        }
    }

    implicitWidth: 1000
    implicitHeight: 550
    focusable: true

    property string apiKey: ""
    property string location: currentConfig.weatherLocation
    property string temperature: ""
    property string condition: ""
    property string icon: "‚õÖ"
    property string humidity: ""
    property string feelsLike: ""
    property string windSpeed: ""
    property string pressure: ""
    property string visibility: ""
    property string uvIndex: ""
    property var forecastDays: []
    property bool isLoading: false
    property string errorMessage: ""
    property bool isSearchingLocation: false
    property var locationSearchResults: []
    property int currentLocationIndex: 0
    property bool isUserSearching: false

    // Timer auto-validate API key
    property Timer apiKeyValidateTimer: Timer {
        interval: 500
        repeat: false
        onTriggered: {
            if (weatherPanel.apiKey !== panelConfig.get("weatherApiKey", "")) {
                saveAndValidateApiKey(weatherPanel.apiKey)
            }
        }
    }

    // Timer debounce location search
    property Timer searchDebounceTimer: Timer {
        interval: 300
        repeat: false
        onTriggered: {
            if (weatherPanel.location.length >= 2 && weatherPanel.isUserSearching) {
                searchLocation(weatherPanel.location)
            }
        }
    }

    // Timer ·∫©n location results
    Timer {
        id: hideResultsTimer
        interval: 200
        repeat: false
        onTriggered: {
            weatherPanel.locationSearchResults = []
            weatherPanel.isUserSearching = false
        }
    }

    anchors {
        top: currentConfig.mainPanelPos === "top"
        bottom: currentConfig.mainPanelPos === "bottom"
    }

    margins {
        top: currentConfig.mainPanelPos === "top" ? 10 : 0
        bottom: currentConfig.mainPanelPos === "bottom" ? 10 : 0
        left: 400
    }

    exclusiveZone: 0
    color: "transparent"

    // Process t√¨m ki·∫øm ƒë·ªãa ƒëi·ªÉm
    Process {
        id: searchLocationProcess
        running: false
        stdout: StdioCollector {
            onStreamFinished: {
                weatherPanel.isSearchingLocation = false
                weatherPanel.locationSearchResults = []
                if (text && text.length > 0) {
                    try {
                        const data = JSON.parse(text)
                        if (!data.error) {
                            weatherPanel.locationSearchResults = data
                            if (data.length > 0) {
                                weatherPanel.currentLocationIndex = 0
                            }
                        }
                    } catch(e) {}
                }
            }
        }
    }

    // Process l·∫•y weather forecast
    Process {
        id: weatherProcess
        running: false
        stdout: StdioCollector {
            onStreamFinished: {
                weatherPanel.isLoading = false
                if (text && text.length > 0) {
                    try {
                        const data = JSON.parse(text)
                        if (data.error) {
                            weatherPanel.errorMessage = data.error.message
                        } else {
                            weatherPanel.processWeatherData(data)
                            weatherPanel.errorMessage = ""
                        }
                    } catch(e) {
                        weatherPanel.errorMessage = "Kh√¥ng th·ªÉ ph√¢n t√≠ch d·ªØ li·ªáu th·ªùi ti·∫øt"
                    }
                } else if (weatherPanel.apiKey === "") {
                    weatherPanel.errorMessage = "Vui l√≤ng nh·∫≠p API key"
                }
            }
        }
    }

    function processWeatherData(data) {
        if (data.current) {
            temperature = `${Math.round(data.current.temp_c)}¬∞C`
            condition = data.current.condition.text
            humidity = `${data.current.humidity}%`
            feelsLike = `${Math.round(data.current.feelslike_c)}¬∞C`
            windSpeed = `${data.current.wind_kph} km/h`
            pressure = `${data.current.pressure_mb} mb`
            visibility = `${data.current.vis_km} km`
            uvIndex = data.current.uv.toString()
            icon = getWeatherIcon(data.current.condition.code, data.current.is_day)
        }

        if (data.forecast && data.forecast.forecastday) {
            const forecast = []
            for (let i = 0; i < data.forecast.forecastday.length; i++) {
                const day = data.forecast.forecastday[i]
                forecast.push({
                    date: day.date,
                    dateText: formatDate(day.date),
                    dayName: getDayName(day.date),
                    maxTemp: Math.round(day.day.maxtemp_c),
                    minTemp: Math.round(day.day.mintemp_c),
                    condition: day.day.condition.text,
                    icon: getWeatherIcon(day.day.condition.code, true),
                    rainChance: day.day.daily_chance_of_rain
                })
            }
            forecastDays = forecast
        }
    }

    function formatDate(dateStr) {
        const date = new Date(dateStr)
        return `${date.getDate()}/${date.getMonth() + 1}`
    }

    function getDayName(dateStr) {
        const date = new Date(dateStr)
        const today = new Date()
        const tomorrow = new Date(today)
        tomorrow.setDate(tomorrow.getDate() + 1)

        if (date.toDateString() === today.toDateString()) return lang?.dateFormat?.today || "H√¥m nay"
        if (date.toDateString() === tomorrow.toDateString()) return lang?.dateFormat?.tomorrow || "Ng√†y mai"

        const weekdays = lang?.dateFormat?.day
        const days = weekdays ? [
            weekdays.sunday || "CN", weekdays.monday || "T2", weekdays.tuesday || "T3",
            weekdays.wednesday || "T4", weekdays.thursday || "T5",
            weekdays.friday || "T6", weekdays.saturday || "T7"
        ] : ["CN", "T2", "T3", "T4", "T5", "T6", "T7"]
        return days[date.getDay()]
    }

    function getWeatherIcon(code, isDay) {
        const iconMap = {
            "1000": isDay ? "‚òÄÔ∏è" : "üåô", "1003": isDay ? "‚õÖ" : "‚òÅÔ∏è", "1006": "‚òÅÔ∏è",
            "1009": "üå´Ô∏è", "1030": "üå´Ô∏è", "1063": "üå¶Ô∏è", "1066": "üå®Ô∏è", "1087": "‚õàÔ∏è",
            "1183": "üåßÔ∏è", "1186": "üåßÔ∏è", "1273": "‚õàÔ∏è", "1276": "‚õàÔ∏è", "1279": "‚õàÔ∏è", "1282": "‚õàÔ∏è"
        }
        return iconMap[code.toString()] || "üåà"
    }

    function saveAndValidateApiKey(key) {
        if (key === "") {
            errorMessage = "Vui l√≤ng nh·∫≠p API key"
            return
        }
        panelConfig.set("weatherApiKey", key)
        weatherPanel.apiKey = key
        updateWeather()
    }

    function searchLocation(query) {
        if (query === "" || apiKey === "") {
            locationSearchResults = []
            isSearchingLocation = false
            return
        }
        try { searchLocationProcess.running = false } catch(e) {}
        isSearchingLocation = true
        const url = `https://api.weatherapi.com/v1/search.json?key=${apiKey}&q=${encodeURIComponent(query)}`
        searchLocationProcess.command = ["curl", "-s", url]
        searchLocationProcess.running = true
    }

    function selectLocation(locationName) {
        panelConfig.set("weatherLocation", locationName)
        location = locationName
        locationSearchResults = []
        currentLocationIndex = 0
        isUserSearching = false
        updateWeather()
    }

    function updateWeather() {
        if (apiKey === "") {
            errorMessage = "Vui l√≤ng nh·∫≠p API key"
            return
        }
        isLoading = true
        errorMessage = ""
        const url = `https://api.weatherapi.com/v1/forecast.json?key=${apiKey}&q=${encodeURIComponent(location)}&days=3&lang=${currentConfig.lang}`
        weatherProcess.command = ["curl", "-s", url]
        weatherProcess.running = true
    }

    // Main UI
    Rectangle {
        anchors.fill: parent
        radius: 20
        border.color: theme.button.border
        border.width: 3

        color: theme.primary.background

        ColumnLayout {
            anchors.fill: parent
            anchors.margins: 20
            spacing: 20

            // Header
            Com.WeatherHeader {}

            // Main content - 2 columns
            RowLayout {
                Layout.fillWidth: true
                Layout.fillHeight: true
                spacing: 20

                // Left: Config
                Com.WeatherConfigSection {
                    apiKey: weatherPanel.apiKey
                    location: weatherPanel.location
                    isSearchingLocation: weatherPanel.isSearchingLocation
                    locationSearchResults: weatherPanel.locationSearchResults
                    currentLocationIndex: weatherPanel.currentLocationIndex
                    isUserSearching: weatherPanel.isUserSearching
                    errorMessage: weatherPanel.errorMessage

                    onApiKeyEdited: function(newKey) {
                        weatherPanel.apiKey = newKey
                        weatherPanel.apiKeyValidateTimer.restart()
                    }

                    onLocationTextEdited: function(newText) {
                        weatherPanel.location = newText
                        weatherPanel.searchDebounceTimer.stop()
                        if (newText.length >= 2) {
                            weatherPanel.searchDebounceTimer.restart()
                        } else {
                            weatherPanel.locationSearchResults = []
                            weatherPanel.currentLocationIndex = 0
                        }
                    }

                    onLocationFocusStatusChanged: function(hasFocus) {
                        if (hasFocus) {
                            weatherPanel.isUserSearching = true
                        } else {
                            hideResultsTimer.restart()
                        }
                    }

                    onSearchLocationRequested: function(query) {
                        weatherPanel.searchLocation(query)
                    }

                    onLocationSelected: function(locationName) {
                        weatherPanel.selectLocation(locationName)
                    }
                }

                // Right: Weather Display
                Rectangle {
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    Layout.preferredWidth: parent.width * 0.6
                    radius: 16
                    color: theme.primary.background

                    ColumnLayout {
                        anchors.fill: parent
                        anchors.margins: 1
                        spacing: 20

                        // Current weather
                        Com.WeatherCurrentDisplay {
                            Layout.fillWidth: true
                            Layout.preferredHeight: parent.height/2
                            temperature: weatherPanel.temperature
                            condition: weatherPanel.condition
                            icon: weatherPanel.icon
                            feelsLike: weatherPanel.feelsLike
                            humidity: weatherPanel.humidity
                            windSpeed: weatherPanel.windSpeed
                            pressure: weatherPanel.pressure
                            visibility: weatherPanel.visibility
                            uvIndex: weatherPanel.uvIndex
                            hasData: weatherPanel.temperature !== "" && weatherPanel.errorMessage === ""
                        }

                        // 7-day forecast
                        Com.WeatherForecastList {
                            Layout.preferredHeight: parent.height/2
                            theme: weatherPanel.theme
                            forecastDays: weatherPanel.forecastDays
                        }
                    }
                }
            }
        }
    }

    // Keyboard shortcuts
    Shortcut {
        sequence: "Up"
        enabled: locationSearchResults.length > 0
        onActivated: {
            if (currentLocationIndex > 0) currentLocationIndex--
            else currentLocationIndex = locationSearchResults.length - 1
        }
    }

    Shortcut {
        sequence: "Down"
        enabled: locationSearchResults.length > 0
        onActivated: {
            if (currentLocationIndex < locationSearchResults.length - 1) currentLocationIndex++
            else currentLocationIndex = 0
        }
    }

    Shortcut {
        sequence: "Return"
        enabled: locationSearchResults.length > 0
        onActivated: {
            var item = locationSearchResults[currentLocationIndex]
            if (item) selectLocation(`${item.name},${item.country}`)
        }
    }
}
